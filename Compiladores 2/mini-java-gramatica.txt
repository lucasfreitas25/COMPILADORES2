PROG          ->  public class id {  public static void main ( String [ ] id ) {  <CMDS> } <METODO> } 
METODO        ->  public static <TIPO> id ( <PARAMS> )  { <DC> <CMDS> return <EXPRESSAO> ; } | λ
PARAMS        ->  <TIPO> id<MAIS_PARAMS> | λ
MAIS_PARAMS   -> , <PARAMS> | λ
DC            -> <VAR> <MAIS_DC>  | λ
MAIS_DC       -> ; <DC> | λ
VAR           ->  <TIPO> <VARS> ;  
VARS          -> id<MAIS_VAR>
MAIS_VAR      -> ,<VARS> | λ
TIPO          ->  double
CMDS          -> <CMD><MAIS_CMDS> | λ
MAIS_CMDS     -> ; <CMDS> | λ
CMD           ->  if (  <CONDICAO> )  {<CMDS>} <PFALSA>
                  | while (  <CONDICAO> )  {<CMDS>}
                  | System.out.println (<EXPRESSAO>) 
                  | id <RESTO_IDENT> 
PFALSA        -> else { <CMDS> } | λ
RESTO_IDENT   -> = <EXP_IDENT> | (<LISTA_ARG>)
LISTA_ARG     -> <ARGUMENTOS> | λ
ARGUMENTOS    -> id <MAIS_IDENT>
MAIS_IDENT    -> , <ARGUMENTOS> | λ
EXP_IDENT     -> <EXPRESSAO> | lerDouble()
CONDICAO      -> <EXPRESSAO> <RELACAO> <EXPRESSAO>
RELACAO       -> == | != | >= | <= | > | <
EXPRESSAO     -> <TERMO> <OUTROS_TERMOS>
TERMO         -> <OP_UN> <FATOR> <MAIS_FATORES>
OP_UN         -> - | λ
FATOR         -> id | numero_real | (<EXPRESSAO>)
OUTROS_TERMOS -> <OP_AD> <TERMO> <OUTROS_TERMOS> | λ
OP_AD         -> + | -
MAIS_FATORES  -> <OP_MUL> <FATOR> <MAIS_FATORES> | λ
OP_MUL        -> * | /